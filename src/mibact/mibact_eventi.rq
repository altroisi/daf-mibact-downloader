select * where {
SELECT DISTINCT 
?s AS ?evento_uri
?evento_id
?nome
?descrizione
?latitudine
?longitudine
?tipo_evento
?prenotazione
?copertina_url
?locandina_url
?comunicato_stampa_url
?contact_point_tipo
?contact_point_telefono
?contact_point_fax
?contact_point_email
?contact_point_website
?contact_point_orari
?sede_indirizzo
?sede_cap
?sede_comune 
?sede_provincia
?data_inizio
?data_fine
?raffigurazione_url
?tipo_biglietto
?importo_biglietto
FROM <http://dati.beniculturali.it/mibact/eventi>
FROM <http://dati.beniculturali.it/mibact/luoghi>
WHERE {
?s rdf:type cis:Event .
OPTIONAL { ?s cis:identifier ?evento_id }
OPTIONAL { ?s cis:name ?nome}
OPTIONAL { ?s rdfs:comment ?descrizione}
OPTIONAL { ?s geo:lat ?latitudine }
OPTIONAL { ?s geo:long ?longitudine }
OPTIONAL { ?s dc:type ?tipo_evento}
OPTIONAL {
    ?s cis:hasAccessCondition [
        rdf:type cis:Booking ; 
        rdfs:label ?prenotazione
    ] 
}
OPTIONAL {
    ?s cis:isSubjectOf ?creativeWork . 
    OPTIONAL { ?creativeWork rdfs:label ?CreativeWorkLabel } 
    OPTIONAL { ?creativeWork cis:url ?copertina_url } 
    FILTER(REGEX(?CreativeWorkLabel,'Copertina','i'))
}
OPTIONAL {
    ?s cis:isSubjectOf ?creativeWork2 . 
    OPTIONAL { ?creativeWork2 rdfs:label ?CreativeWorkLabel2 } 
    OPTIONAL { ?creativeWork2 cis:url ?locandina_url } 
    FILTER(REGEX(?CreativeWorkLabel2,'Locandina','i'))
}
OPTIONAL {
    ?s cis:isSubjectOf ?creativeWork3 . 
    OPTIONAL { ?creativeWork3 rdfs:label ?CreativeWorkLabel3 } 
    OPTIONAL { ?creativeWork3 cis:url ?comunicato_stampa_url } 
    FILTER(REGEX(?CreativeWorkLabel3,'Comunicato stampa','i'))
}

OPTIONAL {
    ?s cis:hasContactPoint ?contactPoint .
    OPTIONAL { ?contactPoint rdfs:label ?contact_point_tipo }
    OPTIONAL { ?contactPoint cis:hasTelephone ?contact_point_telefono }
    OPTIONAL { ?contactPoint cis:hasFax ?contact_point_fax }
    OPTIONAL { ?contactPoint cis:hasEmail ?contact_point_email }
    OPTIONAL { ?contactPoint cis:hasWebSite ?contact_point_website }
    OPTIONAL { 
        ?contactPoint cis:available ?available .
        OPTIONAL { ?available cis:description ?contact_point_orari }
    }
}
OPTIONAL {
?s cis:isHostedBy ?host .
OPTIONAL {
    ?host cis:hasAddress ?address . ?address rdfs:label ?address_label .
    OPTIONAL { ?address cis:fullAddress ?sede_indirizzo   }
    OPTIONAL { ?address cis:postCode ?sede_cap } 
    OPTIONAL { ?address cis:postName ?sede_comune         }                       
    OPTIONAL { ?address cis:adminUnitL2 ?sede_provincia   }
}
}
OPTIONAL {
    ?s cis:takesPlaceDuring ?takesPlaceDuring .
    OPTIONAL { ?takesPlaceDuring cis:startDate ?data_inizio }
    OPTIONAL { ?takesPlaceDuring cis:endDate ?data_fine}
}
OPTIONAL { ?s foaf:depiction ?raffigurazione_url}
OPTIONAL { 
    ?s cis:hasTicket ?ticket .   
    ?offer cis:includes ?ticket .
    ?offer rdfs:label ?tipo_biglietto .
    ?offer cis:hasPriceSpecification ?priceSpec .
    ?priceSpec cis:hasCurrencyValue ?importo_biglietto
}          
#FILTER regex(?Identifier,'DBUnico.140177','i')
}
ORDER BY ?s
}
LIMIT 100
OFFSET 0